if (Boolean.valueOf(release) == false) {
    version = version + '-SNAPSHOT'
}
group 'com.github.newnewcoder'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}
apply plugin: 'com.jfrog.bintray'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

ext {
    archivesBaseName = 'jboss-curl-gradle.plugin'
    description = 'It\'s a gradle plugin process jboss json api to deploy single war.'
}

dependencies {
    def httpclientVersion = '4.5.3'
    compile gradleApi()
    compile localGroovy()
    compile "org.apache.httpcomponents:httpclient:${httpclientVersion}"
    compile "org.apache.httpcomponents:fluent-hc:${httpclientVersion}"
    compile "org.apache.httpcomponents:httpmime:${httpclientVersion}"

    testCompile 'junit:junit:4.11'
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'generic'
        name = 'jboss-curl-gradle-plugin'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/newnewcoder/jboss-curl-gradle-plugin.git'
        labels = ['JBossEAP', 'JBossWildly', 'deploy', 'gradle plugin']
        publish = true
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = project.description
            released = new Date()
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4'
}

apply plugin: 'application'
mainClassName = 'com.github.newnewcoder.jbosscurl.operator.TestHttpclient'

task execute(type: JavaExec) {
    main = mainClassName
    args = []
    classpath = sourceSets.main.runtimeClasspath
}